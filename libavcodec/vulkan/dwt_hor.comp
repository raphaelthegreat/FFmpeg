#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_debug_printf : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(scalar, buffer_reference, buffer_reference_align = 4) buffer DwtCoef {
    int coef_buf[];
};

struct Plane {
    ivec2 dim;
    int coef_stride;
    int pad;
};

#define VC2_TRANSFORM_HAAR (0)
#define VC2_TRANSFORM_5_3 (1)

layout(push_constant, scalar) uniform ComputeInfo {
    int s;
    int diff_offset;
    int level;
    int wavelet_type;
    Plane planes[3];
    DwtCoef dst_buf[3];
    DwtCoef src_buf[3];
};

void main() {
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);

    /* For horizontal synth pass, each invocation handles a horizontal pair of pixels */
    uint plane_idx = gl_GlobalInvocationID.z;
    ivec2 coord_x = coord << ivec2(1, 0);
    ivec2 work_area = planes[plane_idx].dim >> level;
    ivec2 dwt_area = work_area >> 1;
    int start = planes[plane_idx].coef_stride * coord_x.y;
    if (wavelet_type == VC2_TRANSFORM_HAAR) {
        if (any(greaterThanEqual(coord_x, work_area))) {
            return;
        }
        int a = src_buf[plane_idx].coef_buf[start + coord_x.x];
        int b = src_buf[plane_idx].coef_buf[start + coord_x.x + 1];
        int dst_b = (b - a) * (1 << s);
        int dst_a = a * (1 << s) + ((dst_b + 1) >> 1);
        dst_buf[plane_idx].coef_buf[start + coord_x.x] = dst_a;
        dst_buf[plane_idx].coef_buf[start + coord_x.x + 1] = dst_b;
    } else {
        if (any(greaterThanEqual(coord, dwt_area))) {
            return;
        }
        // Lifting stage 2
        if (coord.x < dwt_area.x - 1) {
            int a = src_buf[plane_idx].coef_buf[start + 2 * coord.x + 1];
            int b = src_buf[plane_idx].coef_buf[start + 2 * coord.x];
            int c = src_buf[plane_idx].coef_buf[start + 2 * (coord.x + 1)];
            a -= (b + c + 1) >> 1;
            dst_buf[plane_idx].coef_buf[start + 2 * coord.x + 1] = a;
            dst_buf[plane_idx].coef_buf[start + 2 * coord.x] = b;
            dst_buf[plane_idx].coef_buf[start + 2 * (coord.x + 1)] = b;
        } else {
            int a = src_buf[plane_idx].coef_buf[start + dwt_area.x - 1];
            int b = src_buf[plane_idx].coef_buf[start + dwt_area.x - 2];
            a -= (2 * b + 1) >> 1;
            dst_buf[plane_idx].coef_buf[start + dwt_area.x - 1] = a;
        }

        // Lifting stage 1
        if (coord.x == 0) {
            dst_buf[plane_idx].coef_buf[start] += (2 * dst_buf[plane_idx].coef_buf[start + 1] + 2) >> 2;
        } else if (coord.x < dwt_area.x - 1) {
            int a = dst_buf[plane_idx].coef_buf[start + 2 * coord.x];
            int b = dst_buf[plane_idx].coef_buf[start + 2 * coord.x - 1];
            int c = dst_buf[plane_idx].coef_buf[start + 2 * coord.x + 1];
            a += (b + c + 2) >> 2;
            dst_buf[plane_idx].coef_buf[start + 2 * coord.x] = a;
            dst_buf[plane_idx].coef_buf[start + 2 * coord.x - 1] = b;
            dst_buf[plane_idx].coef_buf[start + 2 * coord.x + 1] = c;
        } else {
            int a = dst_buf[plane_idx].coef_buf[start + dwt_area.x - 2];
            int b = dst_buf[plane_idx].coef_buf[start + dwt_area.x - 1];
            int c = dst_buf[plane_idx].coef_buf[start + dwt_area.x - 3];
            a += (c + b + 2) >> 2;
            dst_buf[plane_idx].coef_buf[start + dwt_area.x - 2] = a;
        }
    }
}
