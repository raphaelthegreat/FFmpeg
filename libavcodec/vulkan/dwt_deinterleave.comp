#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_debug_printf : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(scalar, buffer_reference, buffer_reference_align = 4) buffer DwtCoef {
    uint coef_buf[];
};

struct Plane {
    ivec2 dim;
    int coef_stride;
    int pad;
};

#define VC2_TRANSFORM_HAAR (0)
#define VC2_TRANSFORM_5_3 (1)

layout(push_constant, scalar) uniform ComputeInfo {
    int s;
    int diff_offset;
    int level;
    int wavelet_type;
    Plane planes[3];
    DwtCoef src_buf[3];
    DwtCoef dst_buf[3];
};

void main() {
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
    uint plane_idx = gl_GlobalInvocationID.z;
    ivec2 work_area = planes[plane_idx].dim >> level;
    if (any(greaterThanEqual(coord, work_area))) {
        return;
    }
    int stride = planes[plane_idx].coef_stride;
    ivec2 synth_size = work_area >> 1;
    ivec2 subband = coord & ivec2(1);
    ivec2 new_coord = subband * synth_size + (coord >> 1);
    uint texel = src_buf[plane_idx].coef_buf[coord.y * stride + coord.x];
    dst_buf[plane_idx].coef_buf[new_coord.y * stride + new_coord.x] = texel;
}
